
* Title page menu
*
TPMENU
 FCB 20,12   column, row
 FDB TT1-*
 FCB 3
 FDB MRGB-*
 FCB 20,14
 FDB TT2-*
 FCB 9
 FDB MCMP-*
 FDB $FFFF
TT1
 FCC "RGB",0
TT2
 FCC "Composite",0

* Main menu
*
MNMENU
 FCB 6,5 column, row
 FDB MT1-*
 FCB 15
 FDB MM1-*
 FCB 6,7
 FDB MT2-*
 FCB 15
 FDB MM2-*
 FCB 6,9
 FDB MT3-*
 FCB 15
 FDB MM3-*
 FCB 6,11
 FDB MT4-*
 FCB 15
 FDB MM4-*
 FCB 6,13
 FDB MT5-*
 FCB 15
 FDB MM5-*
 FCB 6,15
 FDB MT6-*
 FCB 15
 FDB MM6-*
 FCB 6,17
 FDB MT7-*
 FCB 15
 FDB MM7-*
 FDB $FFFF
MT1
 FCC "Play Solitaire",0
MT2
 FCC "Begin Again",0
MT3
 FCC "Select a Dragon",0
MT4
 FCC "Tournament Play",0
MT5
 FCC "Challenge Match",0
MT6
 FCC "Peek and Forfeit",0
MT7
 FCC "Return to Game",0

* Game screen menu
*
GMMENU
 FCB 9,0 column, row
 FDB GT1-* "Menu"
 FCB 4
 FDB GM1-*
 FCB 17,0
 FDB GT2-* "Undo"
 FCB 4
 FDB GM2-*
 FCB 25,0
 FDB GT3-* "Find"
 FCB 4
 FDB GM3-*
 FCB 35,0
 FDB GT4-* "Cancel"
 FCB 6
 FDB GM4-*
 FDB $FFFF
GT1
 FCC "Menu",0
GT2
 FCC "Undo",0
GT3
 FCC "Find",0
GT4
 FCC "Cancel",0

* Dragon menu
*
DGMENU
 FCB 6,5 pairs to kong
 FDB A@-*
 FCB 15
 FDB DM1-*
 FCB 6,7 fours galore
 FDB B@-*
 FCB 15
 FDB DM2-*
 FCB 6,9 four winds
 FDB C@-*
 FCB 15
 FDB DM3-*
 FCB 6,11 bam bam
 FDB D@-*
 FCB 15
 FDB DM4-*
 FCB 6,13 crak king
 FDB E@-*
 FCB 15
 FDB DM5-*
 FCB 6,15 dots across
 FDB F@-*
 FCB 15
 FDB DM6-*
 FCB 6,17 dragon rider
 FDB G@-*
 FCB 15
 FDB DM7-*
 FCB 6,19 dragon's song
 FDB H@-*
 FCB 15
 FDB DM8-*
 FCB 6,21 return to menu
 FDB I@-*
 FCB 15
 FDB DM9-*
 FDB $FFFF
A@
 FCC "Pairs to Kong",0
B@
 FCC "Fours Galore",0
C@
 FCC "Four Winds",0
D@
 FCC "Bam Bam",0
E@
 FCC "Crak King",0
F@
 FCC "Dots Across",0
G@
 FCC "Dragon Rider",0
H@
 FCC "Dragon's Song",0
I@
 FCC "Return to Menu",0

* Peek menu
*
PKMENU
 FCB 20,12   column, row
 FDB A@-*
 FCB 3
 FDB PM2-*
 FCB 20,14
 FDB B@-*
 FCB 2
 FDB PM1-*
 FDB $FFFF
A@
 FCC "Yes",0
B@
 FCC "No",0

* Challenge menu
*
CHMENU
 FCB 6,5 column, row
 FDB A@-*
 FCB 15
 FDB CH1-*
 FCB 6,7
 FDB B@-*
 FCB 15
 FDB CH2-*
 FCB 6,9
 FDB C@-*
 FCB 15
 FDB CH3-*
 FCB 6,11
 FDB D@-*
 FCB 15
 FDB CH4-*
 FCB 6,13
 FDB E@-*
 FCB 15
 FDB CH5-*
 FDB $FFFF
A@
 FCC "10 seconds",0
B@
 FCC "20 seconds",0
C@
 FCC "30 seconds",0
D@
 FCC "60 seconds",0
E@
 FCC "Return to Menu",0

* Challenge match game screen menu
*
CGMENU
 FCB 9,0 column, row
 FDB A@-* "Menu"
 FCB 4
 FDB CG1-*
 FCB 17,0
 FDB B@-* "Undo"
 FCB 4
 FDB CG2-*
 FCB 25,0
 FDB C@-* "Find"
 FCB 4
 FDB CG3-*
 FCB 35,0
 FDB D@-* "Cancel"
 FCB 6
 FDB CG4-*
 FDB $FFFF
A@
 FCC "Menu",0
B@
 FCC "Undo",0
C@
 FCC "Find",0
D@
 FCC "Cancel",0

* Tournament menu
*
TOMENU
 FCB 6,5 column, row
 FDB A@-*
 FCB 15
 FDB TO1-*
 FCB 6,7
 FDB B@-*
 FCB 15
 FDB TO2-*
 FCB 6,9
 FDB C@-*
 FCB 15
 FDB TO3-*
 FDB $FFFF
A@
 FCC "Begin 5 minute tournament",0
B@
 FCC "Begin 10 minute tournament",0
C@
 FCC "Return to Menu",0


* Tournament game screen menu
*
TGMENU
 FCB 9,0 column, row
 FDB A@-* "Quit"
 FCB 4
 FDB TG1-*
 FCB 17,0
 FDB B@-* "Undo"
 FCB 4
 FDB TG2-*
 FCB 25,0
 FDB C@-* "Find"
 FCB 4
 FDB TG3-*
 FCB 35,0
 FDB D@-* "Cancel"
 FCB 6
 FDB TG4-*
 FDB $FFFF
A@
 FCC "Quit",0
B@
 FCC "Undo",0
C@
 FCC "Find",0
D@
 FCC "Cancel",0

* Tournament scoreboard menu
*
SCMENU
 FCB 32,15   column, row
 FDB A@-*
 FCB 3
 FDB SC1-*
 FCB 37,15
 FDB B@-*
 FCB 2
 FDB SC2-*
 FDB $FFFF
A@
 FCC "Yes",0
B@
 FCC "No",0

* Draw menu
*
* U -> menu table
*
DMENU
 PSHS U
 STU CURMNU
 CLRA
 CLRB
 STD MNUPTR
DMENU0
 LDD ,U++
 BLT XDMENU
 LBSR CPOS   get position
 LDD ,U
 PSHS U
 LEAU D,U
 LBSR MSG    draw the item
 PULS U
 LEAU 5,U
 BRA DMENU0

XDMENU
 PULS U
 RTS

*convert text col,row to voffset
*
* A is col, B is row
* returns with voffset in X
*
CPOS
 PSHS A    save column
 LDX #SCREEN

 PSHS B   row
CPOSL
 DEC ,S
 BLT XCPOSL
 LEAX 160*8,X
 BRA CPOSL

XCPOSL
 LEAS 1,S

 PULS A   get column back
 LDB #3
 MUL
 LEAX D,X
 RTS

* Update menu
*
* U -> menu table
*
UMENU
 LDA CURSXY+1
 LDB #3
 MUL
 STD TEMPY
 LDA CURSXY
 LDB #5
 MUL
 STD TEMPX
UMENUL
 LDD ,U     column, row
 LBLT XUMENU
* figure out x,y range for menu item
 PSHS A save column
 LDA #8 convert row to character pos
 MUL
 STD ITEMY1
 ADDB #8
 STD ITEMY2
 PULS A get column back
 LDB #6 convert column
 MUL
 STD ITEMX1
 LDA 4,U len of message
 LDB #6
 MUL
 ADDD ITEMX1
 STD ITEMX2

* is the cursor within the range?
 LDD TEMPX
 CMPD ITEMX1
 BLO UMENUX
 CMPD ITEMX2
 BHI UMENUX
 LDD TEMPY
 CMPD ITEMY1
 BLO UMENUX
 CMPD ITEMY2
 BHI UMENUX

* pointing to this item!
 LDD MNUPTR
 BEQ UMENU1
* used to point to something
 PSHS U
 CMPD ,S++
 BNE UMENU0
 RTS         same thing, so forget it
* used to point to something else
* have to de-highlight old item,
* highlight new item
UMENU0
 PSHS U
 LDU MNUPTR
* de-highlight
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 LBSR CUROFF
 LBSR MSG de-highlight
 LBSR CURON
 PULS U
 STU MNUPTR
* highlight
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 INC HFLAG
 LBSR CUROFF
 LBSR MSG highlight
 LBSR CURON
 RTS

* didn't point to anything previously
* so just highlight this item
UMENU1
 STU MNUPTR remember this item
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 INC HFLAG
 LBSR CUROFF
 LBSR MSG highlight
 LBSR CURON
 RTS

UMENUX
* try next menu item
 LEAU 7,U
 LBRA UMENUL

* not pointing to anything
XUMENU
 LDD MNUPTR
 BEQ XXUMNU
* used to point to something
* have to de-highlight old item
 TFR D,U
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 LBSR CUROFF
 LBSR MSG de-highlight
 LBSR CURON
 LDD #0
 STD MNUPTR forget old item

XXUMNU
 RTS

* Execute menu item
XMENU
 LDD MNUPTR
 BEQ XMENUX
 TFR D,U
 LEAU 5,U
 LDD ,U
 LEAU D,U
 LEAS 2,S
 JMP ,U

XMENUX
 RTS
