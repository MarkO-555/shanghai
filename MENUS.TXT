* Title page menu
*
TPMENU
 FCB 8,21   column, row
 FDB A@-*
 FCB 3
 FDB MRGB-*
 FCB 20,21
 FDB B@-*
 FCB 9
 FDB MCMP-*
 FDB $FFFF
A@
 FCC "RGB",0
B@
 FCC "Composite",0

* Main menu
*
MNMENU
 FCB 12,8 column, row
 FDB A@-*
 FCB 14
 FDB MM1-*
 FCB 12,10
 FDB B@-*
 FCB 11
 FDB MM2-*
 FCB 12,12
 FDB C@-*
 FCB 15
 FDB MM3-*
 FCB 12,14
 FDB D@-*
 FCB 15
 FDB MM4-*
 FCB 12,16
 FDB E@-*
 FCB 15
 FDB MM5-*
 FCB 12,18
 FDB F@-*
 FCB 14
 FDB MM6-*
 FDB $FFFF
A@
 FCC "Play Solitaire",0
B@
 FCC "Begin Again",0
C@
 FCC "Select a Dragon",0
D@
 FCC "Tournament Play",0
E@
 FCC "Challenge Match",0
F@
 FCC "Return to Game",0

* Game screen menu
*
GMMENU
 FCB 4,0 column, row
 FDB A@-* "Menu"
 FCB 4
 FDB GM1-*
 FCB 10,0
 FDB B@-* "Undo"
 FCB 4
 FDB GM2-*
 FCB 16,0
 FDB C@-* "Find"
 FCB 4
 FDB GM3-*
 FCB 22,0
 FDB D@-* "Cancel"
 FCB 6
 FDB GM4-*
 FCB 30,0
 FDB E@-* "Peek"
 FCB 4
 FDB GM5-*
 FDB $FFFF
A@
 FCC "Menu",0
B@
 FCC "Undo",0
C@
 FCC "Find",0
D@
 FCC "Cancel",0
E@
 FCC "Peek",0

* Dragon menu
*
DGMENU
 FCB 12,5 pairs to kong
 FDB A@-*
 FCB 13
 FDB DM1-*
 FCB 12,7 fours galore
 FDB B@-*
 FCB 12
 FDB DM2-*
 FCB 12,9 four winds
 FDB C@-*
 FCB 10
 FDB DM3-*
 FCB 12,11 bam bam
 FDB D@-*
 FCB 7
 FDB DM4-*
 FCB 12,13 crak king
 FDB E@-*
 FCB 9
 FDB DM5-*
 FCB 12,15 dots across
 FDB F@-*
 FCB 11
 FDB DM6-*
 FCB 12,17 dragon rider
 FDB G@-*
 FCB 12
 FDB DM7-*
 FCB 12,19 dragon's song
 FDB H@-*
 FCB 13
 FDB DM8-*
 FCB 12,21 return to menu
 FDB I@-*
 FCB 14
 FDB DM9-*
 FDB $FFFF
A@
 FCC "Pairs to Kong",0
B@
 FCC "Fours Galore",0
C@
 FCC "Four Winds",0
D@
 FCC "Bam Bam",0
E@
 FCC "Crak King",0
F@
 FCC "Dots Across",0
G@
 FCC "Dragon Rider",0
H@
 FCC "Dragon's Song",0
I@
 FCC "Return to Menu",0

* Peek menu
*
PKMENU
 FCB 20,12   column, row
 FDB A@-*
 FCB 3
 FDB PM1-*
 FCB 20,14
 FDB B@-*
 FCB 2
 FDB PM2-*
 FDB $FFFF
A@
 FCC "Yes",0
B@
 FCC "No",0

* Challenge menu
*
CHMENU
 FCB 11,9 column, row
 FDB A@-*
 FCB 10
 FDB CH1-*
 FCB 11,11
 FDB B@-*
 FCB 10
 FDB CH2-*
 FCB 11,13
 FDB C@-*
 FCB 10
 FDB CH3-*
 FCB 11,15
 FDB D@-*
 FCB 10
 FDB CH4-*
 FCB 11,17
 FDB E@-*
 FCB 14
 FDB CH5-*
 FDB $FFFF
A@
 FCC "10 seconds",0
B@
 FCC "20 seconds",0
C@
 FCC "30 seconds",0
D@
 FCC "60 seconds",0
E@
 FCC "Return to Menu",0

* Challenge match game screen menu
*
CGMENU
 FCB 4,0 column, row
 FDB A@-* "Quit"
 FCB 4
 FDB CG1-*
 FCB 24,0
 FDB B@-* "Cancel"
 FCB 6
 FDB CG2-*
 FDB $FFFF
A@
 FCC "Quit",0
B@
 FCC "Cancel",0

* Tournament menu
*
TOMENU
 FCB 6,8 column, row
 FDB A@-*
 FCB 15
 FDB TO1-*
 FCB 6,10
 FDB B@-*
 FCB 15
 FDB TO2-*
 FCB 6,12
 FDB C@-*
 FCB 15
 FDB TO3-*
 FDB $FFFF
A@
 FCC "Begin 5 minute tournament",0
B@
 FCC "Begin 10 minute tournament",0
C@
 FCC "Return to Menu",0

* Tournament game screen menu
*
TGMENU
 FCB 4,0 column, row
 FDB A@-* "Quit"
 FCB 4
 FDB TG1-*
 FCB 10,0
 FDB B@-* "Undo"
 FCB 4
 FDB TG2-*
 FCB 22,0
 FDB C@-* "Cancel"
 FCB 6
 FDB TG3-*
 FDB $FFFF
A@
 FCC "Quit",0
B@
 FCC "Undo",0
C@
 FCC "Cancel",0

* Tournament scoreboard menu
*
SCMENU
 FCB 28,15   column, row
 FDB A@-*
 FCB 3
 FDB SC1-*
 FCB 33,15
 FDB B@-*
 FCB 2
 FDB SC2-*
 FDB $FFFF
A@
 FCC "Yes",0
B@
 FCC "No",0

* Peek game screen menu
*
PGMENU
 FCB 4,0 column, row
 FDB A@-* "Quit"
 FCB 4
 FDB PG1-*
 FCB 10,0
 FDB B@-* "Undo"
 FCB 4
 FDB PG2-*
 FDB $FFFF
A@
 FCC "Quit",0
B@
 FCC "Undo",0

* Draw menu
*
* U -> menu table
*
DMENU
 PSHS U
 STU CURMNU
 CLRA
 CLRB
 STD MNUPTR
DMENU0
 LDD ,U++
 BLT XDMENU
 LBSR CPOS   get position
 LDD ,U
 PSHS U
 LEAU D,U
 LBSR MSG    draw the item
 PULS U
 LEAU 5,U
 BRA DMENU0

XDMENU
 PULS U
 RTS

*convert text col,row to voffset
*
* A is col, B is row
* returns with voffset in X
*
CPOS
 PSHS A    save column
 LDX #SCREEN

 PSHS B   row
CPOSL
 DEC ,S
 BLT XCPOSL
 LEAX 160*8,X
 BRA CPOSL

XCPOSL
 LEAS 1,S

 PULS A   get column back
 LDB #4
 MUL
 LEAX D,X
 RTS

* Update menu
*
* U -> menu table
*
UMENU
 LDA CURSXY+1
 LDB #3
 MUL
 STD TEMPY
 LDA CURSXY
 LDB #5
 MUL
 STD TEMPX
UMENUL
 LDD ,U     column, row
 LBLT XUMENU
* figure out x,y range for menu item
 PSHS A save column
 LDA #8 convert row to character pos
 MUL
 STD ITEMY1
 ADDB #8
 STD ITEMY2
 PULS A get column back
 LDB #8 convert column
 MUL
 STD ITEMX1
 LDA 4,U len of message
 LDB #8
 MUL
 ADDD ITEMX1
 STD ITEMX2

* is the cursor within the range?
 LDD TEMPX
 CMPD ITEMX1
 BLO UMENUX
 CMPD ITEMX2
 BHI UMENUX
 LDD TEMPY
 CMPD ITEMY1
 BLO UMENUX
 CMPD ITEMY2
 BHI UMENUX

* pointing to this item!
 LDD MNUPTR
 BEQ UMENU1
* used to point to something
 PSHS U
 CMPD ,S++
 BNE UMENU0
 RTS         same thing, so forget it
* used to point to something else
* have to de-highlight old item,
* highlight new item
UMENU0
 PSHS U
 LDU MNUPTR
* de-highlight
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 LBSR CUROFF
 LBSR MSG de-highlight
 LBSR CURON
 PULS U
 STU MNUPTR
* highlight
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 INC HFLAG
 LBSR CUROFF
 LBSR MSG highlight
 LBSR CURON
 RTS

* didn't point to anything previously
* so just highlight this item
UMENU1
 STU MNUPTR remember this item
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 INC HFLAG
 LBSR CUROFF
 LBSR MSG highlight
 LBSR CURON
 RTS

UMENUX
* try next menu item
 LEAU 7,U
 LBRA UMENUL

* not pointing to anything
XUMENU
 LDD MNUPTR
 BEQ XXUMNU
* used to point to something
* have to de-highlight old item
 TFR D,U
 LDD ,U++
 LBSR CPOS
 LDD ,U
 LEAU D,U
 LBSR CUROFF
 LBSR MSG de-highlight
 LBSR CURON
 LDD #0
 STD MNUPTR forget old item

XXUMNU
 RTS

* Execute menu item
XMENU
 LDD MNUPTR
 BEQ XMENUX
 TFR D,U
 LEAU 5,U
 LDD ,U
 LEAU D,U
 LEAS 2,S
 JMP ,U

XMENUX
 RTS

* draw menu border
*
BORDER
 INC BORDF
 LDX #SCREEN+4*160+1
 LDB #$44
* upper left corner detail
 STB -160-1,X
 STB -2*160-1,X
 LDA #39
* top 
 PSHS A
A@
 DEC ,S
 BLT B@
 LBSR HLINES
 LDB #'#
 LBSR PUT
 BRA A@
B@
 PULS A
* upper right corner detail
 LDB #$44
 STB -160,X
 STB -2*160,X
* bottom
 LDX #SCREEN+187*160+1
 LDA #39
 PSHS A
C@
 DEC ,S
 BLT D@
 LBSR HLINES
 LDB #'#
 LBSR PUT
 BRA C@
D@
 PULS A
* lower right corner detail
 LDB #$44
 STB 8*160,X
 STB 9*160,X
* left
 LDX #SCREEN+4*160+1
 LDA #24
 PSHS A
E@
 DEC ,S
 BLT F@
 BSR VLINES
 LDB #'#
 LBSR PUT
 LEAX 8*160-4,X
 BRA E@
F@
 PULS A
* lower left corner detail
 LDB #$44
 STB -1,X
*right
 LDX #SCREEN+4*160+1
 LEAX 38*4,X
 LDA #24
 PSHS A
G@
 DEC ,S
 BLT H@
 BSR VLINES
 LDB #'#
 LBSR PUT
 LEAX 8*160-4,X
 BRA G@
H@
 PULS A
 CLR BORDF
 RTS

VLINES
 PSHS X
 LDA #$44
 LDB #8
A@
 STA -1,X
 STA 4,X
 LEAX 160,X
 DECB
 BNE A@
 PULS X,PC

HLINES
 LDD #$4444
 STD -2*160,X
 STD -2*160+2,X
 STD -160,X
 STD -160+2,X
 STD 8*160,X
 STD 8*160+2,X
 STD 9*160,X
 STD 9*160+2,X
 RTS

* text menu
*
* U -> menu table
*
TMENU
 STU CURMNU
 LBSR BORDER
 LDU CURMNU
 LBSR DMENU
A@
 LDD SEED1 randomize
 ADDD #1
 STD SEED1
 LDB #1
 LBSR CURSOR
 LDU CURMNU
 LBSR UMENU
 LDB #1
 LBSR BUTTON
 BEQ A@
 LBSR XMENU
 BRA A@
